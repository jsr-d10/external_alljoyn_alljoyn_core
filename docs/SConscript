# Copyright 2011, Qualcomm Innovation Center, Inc.
# 
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
# 
#        http://www.apache.org/licenses/LICENSE-2.0
# 
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.
# 

import os
Import('env')
# Build docs
if env['DOCS'] == 'html':
    # the target directory 'docs/tmp' is never built this will cause doxygen 
    # to run every time DOCS == 'html'
    env.Doxygen(source='Doxygen_html', target=Dir('tmp'))
    env.Clean('Doxygen_html', Dir('html'))
    #env.Command(Dir('tmp'), 'Doxygen_html', 'doxygen ${SOURCE.file}')
elif env['DOCS'] == 'dev':
    # the target directory 'docs/tmp' is never built this will cause doxygen 
    # to run every time DOCS == 'html'
    env.Doxygen(source='Doxygen_dev', target=Dir('tmp'))
    env.Clean('Doxygen_html', Dir('html'))
elif env['DOCS'] == 'pdf':
    # the target directory 'docs/tmp' is never built this will cause doxygen 
    # to run every time DOCS == 'pdf' making refman.tex a target makes sure this
    # command is run before building the pdf output.
    env.Doxygen(source='Doxygen_pdf', target=[Dir('tmp'), File('./latex/refman.tex')])
    #copy the custom style to the latex folder generated by doxygen.
    Command('./latex/quic.sty','./quic.sty',Copy("$TARGET", "$SOURCE"))
    env.PDF('./latex/refman.pdf', './latex/refman.tex')
    env.Clean('Doxygen_pdf', Dir('latex'))
elif env['DOCS'] == 'sandcastle':
    if not os.environ.has_key('SHFBROOT'):
        print 'Missing required environment build variable SHFBROOT'
        Exit()

    chm_env = env.Clone();

    # import the envrionment variable from the system
    shfbroot = chm_env.Dir(os.environ['SHFBROOT'])

    # choose the correct build flavor
    if chm_env['OS'] == 'win8' or chm_env['OS'] == 'win8_nt':
        if chm_env['CPU'] == 'x86':
            platform = 'Win32'
        elif chm_env['CPU'] == 'x86_64':
            platform = 'x64'
        elif chm_env['CPU'] == 'arm':
            platform = 'ARM'

        if chm_env['VARIANT'] == 'debug':
            variant = 'Debug'
        else:
            variant = 'Release'

        # Because SHFB expects a dll filename instead of the winmd,
        # make a copy, build the doc, then delete the temporary file
        chm_cmd = chm_env.Command('winrt/Help/AllJoyn.chm',
                            ['$DISTDIR/bin/AllJoyn.winmd', '$DISTDIR/bin/AllJoyn.xml'],
                            [
                            Copy('${SOURCE}.dll','${SOURCE}'),
                            ''.join([
                                    'cd alljoyn_core\\docs\\winrt && ',
                                    'msbuild.exe AllJoyn.shfbproj /t:Rebuild /p:',
                                    'Configuration=', variant,
                                    ';Platform=', platform,
                                    ';SHFBROOT="', shfbroot.path, '"'
                                    ]),
                            Delete('${SOURCE}.dll')
                            ]
                            )

        chm_install = chm_env.Install('$DISTDIR/bin', 'winrt/Help/AllJoyn.chm')

        # make sure the chm file is built before trying to install it.
        Depends(chm_install, chm_cmd)

elif env['DOCS'] == 'chm':
    # the target directory 'docs/tmp' is never built this will cause doxygen
    # to run every time DOCS == 'chm'
    env.Doxygen(source='Doxygen_chm', target=Dir('tmp'))
    env.Clean('Doxygen_chm', Dir('chm'))
    env.Install('$DISTDIR/bin', 'html/AllJoyn.chm')
